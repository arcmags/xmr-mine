# Maintainer: anonimal <anonimal at getmonero dot org>
# Contributor: redfish <redfish at galactica dot pw>
# Contributor: Onishin <onishin at onishin dot org>
# Contributor: arcmags <c dot magyar dot ec at gmail dot com>

pkgbase="monero"
pkgname=('monero' 'libmonero-wallet')
pkgver=0.13.0.4
pkgrel=1
pkgdesc="Monero: the secure, private, untraceable currency - release version (includes daemon, wallet and miner)"
license=('custom:Cryptonote')
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url="https://getmonero.org/"
depends=('boost-libs' 'libunwind' 'openssl' 'readline' 'zeromq' 'pcsclite')
makedepends=('cmake' 'boost' 'gtest' 'qt5-tools')
provides=('monero' 'libmonero-wallet')
conflicts=('bitmonero-git' 'libmonero-wallet-git')

# git HEADs sourced in release 0.13.0.4:
_miniupnp=6b9b73a567e351b844f96c077f7b752ea92e298a
_rapidjson=129d19ba7f496df5e33658527a7158c79b99c21c
_unbound=7f23967954736dcaa366806b9eaba7e2bdfede11

source=(
    monero.tar.gz::"https://github.com/monero-project/monero/archive/v${pkgver}.tar.gz"
    miniupnp.tar.gz::"https://github.com/monero-project/miniupnp/archive/${_miniupnp}.tar.gz"
    rapidjson.tar.gz::"https://github.com/Tencent/rapidjson/archive/${_rapidjson}.tar.gz"
    unbound.tar.gz::"https://github.com/monero-project/unbound/archive/${_unbound}.tar.gz"
    boost.patch
    monero.install
)

sha256sums=(
    '93158879f2890021ae75b31f4b81ad8026ce5b626da1f2fc44ab70657bfa512c'
    '4474adb27410fb20ad9943b85272cc1d89ceee078e5329ea11240d1650ae586f'
    '44b007d419ac21b6affec58991e865ee572346ead19b73cf1c3e4e11c7a81273'
    '6989c3ee3ec298ac1dfb5f824470d36ea2d582dad263363ab2e29d4219f4e5c6'
    '16c8ef440c09f6972f361f8fad7331b04105bf79cfe5a7c33f14c9dd1c235a9b'
    'd06398de0e45afdc31aa574d7dc21c753825355d585f49ee06e3a6420bcf6420'
)

_monero="${pkgbase}"
_build="build"

prepare()
{
    mv monero-* monero
    rm -rd monero/external/miniupnp
    mv miniupnp-* monero/external/miniupnp
    rm -rd monero/external/rapidjson
    mv rapidjson-* monero/external/rapidjson
    rm -rd monero/external/unbound
    mv unbound-* monero/external/unbound
    cd monero
    patch -p1 -i ../boost.patch
}

build() {
  cd "${srcdir}/${_monero}"
  CMAKE_FLAGS+=" -DCMAKE_BUILD_TYPE=Release "
  CMAKE_FLAGS+=" -DCMAKE_INSTALL_PREFIX=/usr "
  CMAKE_FLAGS+=" -DBUILD_GUI_DEPS=ON "
  #CMAKE_FLAGS+=" -DCMAKE_LINKER=/usr/bin/ld.gold " # #974 ld segfault on ARM, re-implement if needed
  mkdir -p $_build && cd $_build
  cmake $CMAKE_FLAGS ../
  make
}

# Disable running of tests. Tests should not be needed outside of the development environment.
# Cherry-picking tests that aren't "too long" also defeats the purpose of running tests.
#check() {
#  cd "${srcdir}/${_monero}/${_build}"
#
#  # Temporarily disable some a tests:
#  #  * coretests takes too long (~25000s)
#  #  * libwallet_api_tests fail (Issue #895)
#  #  * unit_tests were run separately above
#  #  * hash-variant2-int-sqrt takes too long
#  CTEST_ARGS+="-E 'core_tests|libwallet_api_tests|unit_tests|hash-variant2-int-sqrt'"
#  echo ">>> NOTE: some tests excluded: $CTEST_ARGS"
#
#  make ARGS="$CTEST_ARGS" test
#}

package_monero() {
  backup=('etc/monerod.conf')
  install=monero.install

  # Uncomment for a debug build
  # options=(!strip debug)
  install -Dm644 "${srcdir}/${_monero}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 "${srcdir}/${_monero}/utils/conf/monerod.conf" "${pkgdir}/etc/monerod.conf"
  install -Dm644 "${srcdir}/${_monero}/utils/systemd/monerod.service" "${pkgdir}/usr/lib/systemd/system/monerod.service"

  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-ancestry" "${pkgdir}/usr/bin/monero-blockchain-ancestry"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-depth" "${pkgdir}/usr/bin/monero-blockchain-depth"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-export" "${pkgdir}/usr/bin/monero-blockchain-export"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-import" "${pkgdir}/usr/bin/monero-blockchain-import"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-mark-spent-outputs" "${pkgdir}/usr/bin/monero-blockchain-mark-spent-outputs"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-usage" "${pkgdir}/usr/bin/monero-blockchain-usage"

  install -Dm755 "${srcdir}/${_monero}/build/bin/monerod" "${pkgdir}/usr/bin/monerod"

  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-gen-trusted-multisig" "${pkgdir}/usr/bin/monero-gen-trusted-multisig"

  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-wallet-cli" "${pkgdir}/usr/bin/monero-wallet-cli"
  install -Dm755 "${srcdir}/${_monero}/build/bin/monero-wallet-rpc" "${pkgdir}/usr/bin/monero-wallet-rpc"

}

package_libmonero-wallet() {
  # NOTE: this is crucial, otherwise stripping breaks the .a archive:
  # monero-core (GUI) fails to link against it (it can't find symbols
  # that are clearly in the library).
  options=(!strip)

  _stage_dir=stagedir

  cd "${srcdir}/${_monero}/${_build}"

  mkdir -p $_stage_dir
  make DESTDIR=$_stage_dir install

  cd $_stage_dir
  find usr/{include,lib} -type f -exec install -D -m 755 {} ${pkgdir}/{} \;
}
