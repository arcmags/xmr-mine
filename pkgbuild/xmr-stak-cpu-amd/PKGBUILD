# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>
pkgname=xmr-stak-cpu-amd
pkgver=2.4.7
pkgrel=1
pkgdesc="Monero CPU miner"
arch=('x86_64')
url='https://github.com/fireice-uk/xmr-stak'
license=('GPL3')
conflicts=('xmr-stak-cpu'
	'xmr-stak-cpu-amd-nvidia'
	'xmr-stak-cpu-nvidia'
    'xmr-stak-git'
    'xmr-stak-cpu-git'
    'xmr-stak-nvidia-git'
    'xmr-stak_cpu')
makedepends=('cmake' 'opencl-headers')
depends=('libmicrohttpd' 'openssl' 'hwloc' 'opencl-amd')
source=("xmr-stak-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
    'no-donate.patch')
md5sums=('3d120c2ea49a504b8b97ed8c8d78821d'
    '9737fc7ffdcc178e4836d1a413c41f06')

prepare() {
    cd "$srcdir/xmr-stak-${pkgver}"
    patch -p1 -i ../no-donate.patch
}

build() {
    cd "$srcdir/xmr-stak-${pkgver}"
    mkdir build
    cd build
    cmake .. \
        -DCUDA_ENABLE=OFF
    make
}

package() {
    cd "$srcdir/xmr-stak-${pkgver}/build"
    install -D -m755 ./bin/xmr-stak "$pkgdir/usr/bin/xmr-stak"
    install -D -m644 ./bin/libxmrstak_opencl_backend.so \
		-t "$pkgdir/usr/lib"
}
