# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>
pkgname=xmr-stak-cpu-nvidia
pkgver=2.5.2
pkgrel=1
pkgdesc="Monero CPU/nvidia all in one miner"
arch=('x86_64')
url='https://github.com/fireice-uk/xmr-stak'
license=('GPL3')
conflicts=('xmr-stak-cpu'
    'xmr-stak-git'
    'xmr-stak-cpu-git'
    'xmr-stak-nvidia-git'
    'xmr-stak_cpu')
makedepends=('cmake' 'gcc6' 'opencl-headers')
depends=('libmicrohttpd' 'openssl' 'hwloc' 'cuda' 'ocl-icd')
source=("xmr-stak-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
    'no-donate.patch')
md5sums=('e23757501cb030a0ecd4d7b77fd3bd93'
    '73a3f89510681a2ea5c6b597f3c27589')

prepare() {
    cd "$srcdir/xmr-stak-${pkgver}"
    patch -p1 -i ../no-donate.patch
}

build() {
    cd "$srcdir/xmr-stak-${pkgver}"
    mkdir build
    cd build
    cmake .. \
        -DCMAKE_C_COMPILER=gcc-6 \
        -DCMAKE_CXX_COMPILER=g++-6 \
        -DOpenCL_ENABLE=OFF
    make
}

package() {
    cd "$srcdir/xmr-stak-${pkgver}/build"
    install -D -m755 ./bin/xmr-stak \
        "$pkgdir/usr/bin/xmr-stak"
    install -D -m644 ./bin/libxmrstak_cuda_backend.so \
        "$pkgdir/usr/lib/libxmrstak_cuda_backend.so"
}
