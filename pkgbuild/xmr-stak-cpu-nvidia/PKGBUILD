# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>
# Note: requires gcc6.
pkgname=xmr-stak-cpu-nvidia
pkgver=2.4.5
pkgrel=1
pkgdesc="Monero CPU/nvidia all in one miner"
arch=('x86_64')
url='https://github.com/fireice-uk/xmr-stak'
license=('GPL3')
conflicts=('xmr-stak-cpu'
    'xmr-stak-git'
    'xmr-stak-cpu-git'
    'xmr-stak-nvidia-git'
    'xmr-stak_cpu')
makedepends=('cmake' 'gcc6' 'opencl-headers')
depends=('libmicrohttpd' 'openssl' 'hwloc' 'cuda' 'ocl-icd')
source=("xmr-stak-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
    'no-donate.patch')
md5sums=('a72cc1074e6215feb9c72e9fabcbf342'
    '9737fc7ffdcc178e4836d1a413c41f06')

prepare() {
    cd "$srcdir/xmr-stak-${pkgver}"
    #patch -p1 -i ../no-donate.patch
}

build() {
    cd "$srcdir/xmr-stak-${pkgver}"
    mkdir build
    cd build
    cmake .. \
        -DCMAKE_C_COMPILER=gcc-6 \
        -DCMAKE_CXX_COMPILER=g++-6 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_LINK_STATIC=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DMICROHTTPD_ENABLE=ON \
        -DOpenSSL_ENABLE=ON \
        -DXMR-STAK_COMPILE=native \
        -DCPU_ENABLE=ON \
        -DHWLOC_ENABLE=ON \
        -DOpenCL_ENABLE=OFF
    make
}

package() {
    cd "$srcdir/xmr-stak-${pkgver}/build"
    install -D -m755 ./bin/xmr-stak \
        "$pkgdir/usr/bin/xmr-stak"
    install -D -m644 ./bin/libxmrstak_cuda_backend.so \
        "$pkgdir/usr/lib/libxmrstak_cuda_backend.so"
}
