# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>
pkgname=xmr-stak-cpu
pkgver=2.7.1
pkgrel=1
pkgdesc="Monero CPU miner"
arch=('x86_64')
url='https://github.com/fireice-uk/xmr-stak'
license=('GPL3')
conflicts=('xmr-stak-cpu-amd'
    'xmr-stak-cpu-amd-nvidia'
    'xmr-stak-cpu-nvidia'
    'xmr-stak-git'
    'xmr-stak-cpu-git'
    'xmr-stak-nvidia-git'
    'xmr-stak_cpu')
makedepends=('cmake')
depends=('libmicrohttpd' 'openssl' 'hwloc')
source=("xmr-stak-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
    'no-donate.patch')
md5sums=('7671a8296c777b11318997d942c786fe'
    '19778549cabaa7aa06a90f3104d1a9ff')

prepare() {
    cd "$srcdir/xmr-stak-$pkgver"
    patch -p1 -i ../no-donate.patch
}

build() {
    cd "$srcdir/xmr-stak-$pkgver"
    mkdir build
    cd build
    cmake .. -DOpenCL_ENABLE=OFF -DCUDA_ENABLE=OFF
    make
}

package() {
    cd "$srcdir/xmr-stak-$pkgver/build"
    install -D -m755 ./bin/xmr-stak "$pkgdir/usr/bin/xmr-stak"
}
