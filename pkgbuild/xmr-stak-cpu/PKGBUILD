# Maintainer: Chris Magyar <c.magyar.ec@gmail.com>
pkgname=xmr-stak-cpu
pkgver=2.4.5
pkgrel=1
pkgdesc="Monero CPU miner"
arch=('x86_64')
url='https://github.com/fireice-uk/xmr-stak'
license=('GPL3')
conflicts=('xmr-stak-cpu-nvidia'
    'xmr-stak-git'
    'xmr-stak-cpu-git'
    'xmr-stak-nvidia-git'
    'xmr-stak_cpu')
makedepends=('cmake')
depends=('libmicrohttpd' 'openssl' 'hwloc')
source=("xmr-stak-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
    'no-donate.patch')
md5sums=('a72cc1074e6215feb9c72e9fabcbf342'
    '9737fc7ffdcc178e4836d1a413c41f06')

prepare() {
    cd "$srcdir/xmr-stak-${pkgver}"
    #patch -p1 -i ../no-donate.patch
}

build() {
    cd "$srcdir/xmr-stak-${pkgver}"
    mkdir build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_LINK_STATIC=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DMICROHTTPD_ENABLE=ON \
        -DOpenSSL_ENABLE=ON \
        -DXMR-STAK_COMPILE=native \
        -DCPU_ENABLE=ON \
        -DHWLOC_ENABLE=ON \
        -DOpenCL_ENABLE=OFF \
        -DCUDA_ENABLE=OFF
    make
}

package() {
    cd "$srcdir/xmr-stak-${pkgver}/build"
    install -D -m755 ./bin/xmr-stak "$pkgdir/usr/bin/xmr-stak"
}
